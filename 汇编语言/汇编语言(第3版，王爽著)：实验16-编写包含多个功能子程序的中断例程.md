![实验16 编写包含多个功能子程序的中断例程](./汇编语言(第3版，王爽著)：实验16-编写包含多个功能子程序的中断例程.assets/17731575-5b8bd2acad203245.png)

### 1. 思路：
> 1. 根据 “第 16 章中的代码示例” 以及 “安装程序” 的套路，仿照着之前的例子，代码写出来不会太难
> 2. 需要注意以下几点：
> a. 在代码段中，“标号”或者是“数据标号”，都代表了该段内的某一个偏移地址。
> b. 对于任何一个内存段来说，段地址可以开始于任何16 字节对齐的地方，**偏移地址则总是从 0x0000 开始递增**。
> c. 在编译阶段，每条指令都被计算并赋予了一个汇编地址，就像它们已经被加载到内存中的某个段里一样。实际上，当编译好的程序加载到物理内存后，它在段内的偏移地址和它在编译阶段的汇编地址是相同的。
> d. 在安装“中断例程”程序时，如果“中断例程”中的**直接定址表**有使用到多个子程序的“标号”，那么我们需要在“中断例程”第一行指令前面，使用 **org	偏移地址** 设定程序段的起始地址，否则“中断例程”中的标号对应的偏移地址，依然还是安装程序在运行时的偏移地址。

> 温馨提示：
> 1. 对于 **标号** 对应的偏移地址有疑问的，请查看 **《x86汇编语言：从实模式到保护模式》(李忠)** 第 5 章的 5.5 小节。
> 2. 使用 **org	偏移地址** 设定程序段的起始地址（即 从“org 偏移地址”开始，后面指令的偏移地址将按照设定的起始地址（偏移地址）为起点开始递增）。


### 2. int 7ch 中断例程代码：
> 文件名：exp16.asm
```
assume cs:code

code segment
    start:
        mov ax, cs
        mov ds, ax
        mov si, offset setscreen                        ; 设置 ds:si 指向源地址
        mov ax, 0
        mov es, ax
        mov di, 200h                                    ; 设置 es:di 指向目的地址
        mov cx, offset setscreenend - offset setscreen  ; 设置 cx 为传输长度
        cld                                             ; 设置传输方向为正
        rep movsb

        ; 设置中断向量表
        mov ax, 0
        mov es, ax
        cli
        mov word ptr es:[7ch * 4], 200h
        mov word ptr es:[7ch * 4 + 2], 0
        sti

        mov ax, 4c00h
        int 21h


    ; 新的 int 7ch 中断例程
        org 200h                        ; 设置 200h 作为以下指令的起始地址，后面指令的偏移地址将按照设定的起始地址（偏移地址）为起点开始递增
    setscreen:
        jmp short set
        table dw sub1, sub2, sub3, sub4
    set:
        push bx
        cmp ah, 3                       ; 判断功能号是否大于 3
        ja sret
        mov bl, ah                      ; ah 寄存器传递功能号
        mov bh, 0
        add bx, bx                      ; 根据 ah 中的功能号，计算对应子程序在 table 表中的偏移（table 表使用 dw 定义字型数据，所以需要 功能号*2）
        call word ptr table[bx]         ; 调用对应的功能子程序
    sret:
        pop bx
        iret

    sub1:
        push bx
        push cx
        push es
        mov bx, 0b800h
        mov es, bx
        mov bx, 0
        mov cx, 2000
    sub1s:
        mov byte ptr es:[bx], ' '
        add bx, 2
        loop sub1s
        pop es
        pop cx
        pop bx
        ret                             ; 清屏

    sub2:
        push bx
        push cx
        push es
        mov bx, 0b800h
        mov es, bx
        mov bx, 1
        mov cx, 2000
    sub2s:
        and byte ptr es:[bx], 11111000b
        or es:[bx], al
        add bx, 2
        loop sub2s
        pop es
        pop cx
        pop bx
        ret                             ; 设置前景色（字符颜色）

    sub3:
        push bx
        push cx
        push es
        mov cl, 4
        shl al, cl
        mov bx, 0b800h
        mov es, bx
        mov bx, 1
        mov cx, 2000
    sub3s:
        and byte ptr es:[bx], 10001111b
        or es:[bx], al
        add bx, 2
        loop sub3s
        pop es
        pop cx
        pop bx
        ret                             ; 设置背景色

    sub4:
        push cx
        push si
        push di
        push es
        push ds
        mov si, 0b800h
        mov es, si
        mov ds, si
        mov si, 160                     ; ds:si 指向第 n+1 行
        mov di, 0                       ; es:di 指向第 n 行
        cld
        mov cx, 24                      ; 共复制 24 行
    sub4s:
        push cx
        mov cx, 160
        rep movsb                       ; 复制一整行，一行有 160 个字节
        pop cx
        loop sub4s
        mov cx, 80
        mov si, 0
    sub4s1:
        mov byte ptr [160*24+si], ' '   ; 最后一行清空
        add si, 2
        loop sub4s1
        pop ds
        pop es
        pop di
        pop si
        pop cx
        ret                             ; 向上滚动一行

    setscreenend:
        nop
code ends

end start
```

### 3. 测试程序代码：
> 文件名：exp16d.asm
#### （1） 清屏
```
assume cs:code

code segment
    start:
        mov ah, 0           ; 0 表示清屏，1 表示设置前景色，2 表示设置背景色，3 表示向上滚动一行
        mov al, 2           ; 对于 1、2 号功能，用 al 传送颜色值，(al)∈{0, 1, 2, 3, 4, 5, 6, 7}
        int 7ch             ; 调用新的 int 7ch 中断例程

        mov ax, 4c00h
        int 21h
code ends

end
```
![清屏 程序运行效果1](./汇编语言(第3版，王爽著)：实验16-编写包含多个功能子程序的中断例程.assets/17731575-f2ed096ea0c2c24f.png)

![清屏 程序运行效果2](./汇编语言(第3版，王爽著)：实验16-编写包含多个功能子程序的中断例程.assets/17731575-50a4cb3fc2562b58.png)


#### （2） 设置前景色
```
assume cs:code

code segment
    start:
        mov ah, 1           ; 0 表示清屏，1 表示设置前景色，2 表示设置背景色，3 表示向上滚动一行
        mov al, 2           ; 对于 1、2 号功能，用 al 传送颜色值，(al)∈{0, 1, 2, 3, 4, 5, 6, 7}
        int 7ch             ; 调用新的 int 7ch 中断例程

        mov ax, 4c00h
        int 21h
code ends

end
```
![设置前景色 程序运行效果](./汇编语言(第3版，王爽著)：实验16-编写包含多个功能子程序的中断例程.assets/17731575-755674e39ce5e21f.png)


#### （3） 设置背景色
```
assume cs:code

code segment
    start:
        mov ah, 2           ; 0 表示清屏，1 表示设置前景色，2 表示设置背景色，3 表示向上滚动一行
        mov al, 2           ; 对于 1、2 号功能，用 al 传送颜色值，(al)∈{0, 1, 2, 3, 4, 5, 6, 7}
        int 7ch             ; 调用新的 int 7ch 中断例程

        mov ax, 4c00h
        int 21h
code ends

end
```
![设置背景色 程序运行效果](./汇编语言(第3版，王爽著)：实验16-编写包含多个功能子程序的中断例程.assets/17731575-493d6656c4b37644.png)


#### （4） 向上滚动一行
```
assume cs:code

code segment
    start:
        mov ah, 3           ; 0 表示清屏，1 表示设置前景色，2 表示设置背景色，3 表示向上滚动一行
        mov al, 2           ; 对于 1、2 号功能，用 al 传送颜色值，(al)∈{0, 1, 2, 3, 4, 5, 6, 7}
        int 7ch             ; 调用新的 int 7ch 中断例程

        mov ax, 4c00h
        int 21h
code ends

end
```
![向上滚动一行 程序运行效果](./汇编语言(第3版，王爽著)：实验16-编写包含多个功能子程序的中断例程.assets/17731575-efaabad2c8953a4c.png)
