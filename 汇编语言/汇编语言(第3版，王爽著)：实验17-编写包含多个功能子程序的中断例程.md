![实验17 编写包含多个功能子程序的中断例程](./汇编语言(第3版，王爽著)：实验17-编写包含多个功能子程序的中断例程.assets/17731575-46233137616a9de5.png)


### 1） 前提知识：
1. 说明
> 1. 我用的是 **DOSBox 0.74-3** 启动并运行的，而且我没有软盘，因此我只能操作硬盘
> 2. 直接向磁盘扇区写入数据是危险的，很可能覆盖掉重要的数据，所以千万不要随便对硬盘中的扇区进行写入。**文章中所有程序的功能都是从硬盘中读取数据**。

2. 从 硬盘中读取数据 的测试代码
> 文件名：pro174.asm
```
assume cs:code

code segment
    ; 程序功能：
    ;     读取 0 面 0 道 1 扇区的内容到 0:200
    ; 入口参数：
    ;     (ah)=13h 的功能号（2 表示读扇区）
    ;     (al)=读取的扇区数
    ;     (ch)=磁道号
    ;     (cl)=扇区号
    ;     (dh)=磁头号（对于软盘即面号，因为一个面用一个磁头来读写）
    ;     (dl)=驱动器号（软驱从 0 开始：0 代表软驱A，1 代表软驱B；硬盘从 80h 开始，80h 代表硬盘C，81h 代表硬盘D）
    ;     es:bx 指向接收从扇区读入数据的内存区
    ; 返回参数：
    ;     操作成功：(ah)=0，(al)=读入的扇区数
    ;     操作失败：(ah)=出错代码
    start:
        mov ax, 0
        mov es, ax
        mov bx, 200h        ; es:bx 指向接收从扇区读入数据的内存区

        mov ah, 2           ; int 13h 的功能号（2 号子程序表示读扇区）
        mov al, 1           ; 读取的扇区数
        mov ch, 0           ; 磁道号
        mov cl, 1           ; 扇区号
        mov dh, 0           ; 磁头号（对于软盘即面号，因为一个面用一个磁头来读写）
        mov dl, 80h         ; 驱动器号（软驱从 0 开始：0 代表软驱A，1 代表软驱B；硬盘从 80h 开始，80h 代表硬盘C，81h 代表硬盘D）
        int 13h

        mov ax, 4c00h
        int 21h
code ends

end start
```

![从 硬盘中读取数据 的测试代码的调试过程](./汇编语言(第3版，王爽著)：实验17-编写包含多个功能子程序的中断例程.assets/17731575-9eadf1377cfa2b7e.png)

![读取硬盘失败的情况](./汇编语言(第3版，王爽著)：实验17-编写包含多个功能子程序的中断例程.assets/17731575-8f3fdb8a92b46062.png)


### 2） 实验17 正文：
1. 思路
> 实验要求是安装一个新的 int 7ch 中断例程，然后将传入的 逻辑扇区号 转化为 int 13h 中断例程需要的入口参数，逻辑扇区号 算出物理编号的算法已经给出，基本是除法操作，相对来说不算太难

2. 实现代码：
> 文件名：exp17.asm
```
assume cs:code

code segment
    start:
        mov ax, cs
        mov ds, ax
        mov si, offset int7ch                       ; 设置 ds:si 指向源地址
        mov ax, 0
        mov es, ax
        mov di, 200h                                ; 设置 es:di 指向目的地址
        mov cx, offset int7chend - offset int7ch    ; 设置 cx 为传输长度
        cld                                         ; 设置传输方向为正
        rep movsb

        ; 设置中断向量表
        mov ax, 0
        mov es, ax
        cli
        mov word ptr es:[7ch * 4], 200h
        mov word ptr es:[7ch * 4 + 2], 0
        sti

        mov ax, 4c00h
        int 21h


    ; 新的 int 7ch 中断例程，实现通过逻辑扇区号对软盘进行读写
    ; 参数说明：
    ; 1. 用 ah 寄存器传递功能号：0 表示读，1 表示写；
    ; 2. 用 dx 寄存器传递要读写的扇区的逻辑扇区号；
    ; 3. 用 es:bx 指向存储读出数据或写入数据的内存区。
    int7ch:
        cmp ah, 1
        ja int7chret         ; 当 ah 传递的功能号大于 1 则转移

        push cx
        push dx
        push si
        push di             ; 保护现场，因为调用 int 13h，ax 会存储读写操作的结果，所以 ax 不需要入栈

        mov di, ax          ; 将传递进来的 功能号保存起来
        mov ax, dx          ; 将 逻辑扇区号 放到 ax 中，用于做除法操作
        mov dx, 0           ; 除数为 16 位，因此被除数为 32 位，需要 ax 和 dx
        mov si, 1440
        div si              ; 除数为 16 位，则 AX 存储除法操作的商，DX 存储除法操作的余数
        mov si, ax          ; 将商保存起来
        mov ax, dx          ; 将余数放到 ax 中，还需要做除法
        mov cl, 18
        div cl              ; 除数为 8 位，则 AL 存储除法操作的商，AH 存储除法操作的余数

        mov ch, al          ; 磁道号
        mov cl, ah
        inc cl              ; 扇区号
        mov ax, di          ; ah 为传递进来的 功能号
        add ah, 2           ; int 13h 的功能号
        mov al, 1           ; 读取的扇区数
        mov dx, si
        mov dh, dl          ; 磁头号（对软盘即面号，因为一个面用一个磁头来读写）
        mov dl, 80h         ; 驱动器号（软驱从 0 开始：0 代表软驱A，1 代表软驱B；硬盘从 80h 开始，80h 代表硬盘C，81h 代表硬盘D）

        int 13h             ; 调用 int 13h 来对磁盘进行读写

    int7chret:
        pop di
        pop si
        pop dx
        pop cx              ; 恢复现场
        iret

    int7chend:
        nop
code ends

end start
```

3. 新的 int 7ch 中断例程测试代码：
> 文件名：exp17d.asm
```
assume cs:code

code segment
    start:
        mov ah, 0                 ; 用 ah 寄存器传递功能号：0 表示读，1 表示写
        mov dx, 0                 ; 用 dx 寄存器传递要读写的扇区的逻辑扇区号
        mov bx, 0
        mov es, bx
        mov bx, 200h              ; 用 es:bx 指向存储读出数据或写入数据的内存区

        int 7ch                   ; 调用新的 int 7ch 中断例程

        mov ax, 4c00h
        int 21h
code ends

end start
```

4. 代码调试示意图：
![实验17 代码调试示意图](./汇编语言(第3版，王爽著)：实验17-编写包含多个功能子程序的中断例程.assets/17731575-59d6ad1997576a83.png)
